

buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["BillieCamera_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return true;
  // return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jetbrains.kotlin.android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["BillieCamera_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["BillieCamera_" + name]).toInteger()
}


def resolveReactNativeDirectory() {
  def reactNativeLocation = rootProject.hasProperty("reactNativeDir") ? rootProject.getProperty("reactNativeDir") : null

  if (reactNativeLocation != null) {
    return file(reactNativeLocation)
  }

  try {
    // Resolve React Native location with Node
    // This will make sure that we get installation location correctly in monorepos
    def reactNativePackageJsonPathStdout = new ByteArrayOutputStream()

    exec {
      commandLine("node", "-p", "require.resolve('react-native/package.json')")
      ignoreExitValue true
      standardOutput = reactNativePackageJsonPathStdout
    }

    def reactNativeFromProjectNodeModules = file(reactNativePackageJsonPathStdout.toString().trim()).getParentFile();

    if (reactNativeFromProjectNodeModules.exists()) {
      return reactNativeFromProjectNodeModules
    }
  } catch (e) {
    // Ignore
  }

  throw new Exception(
          "${project.name}: Failed to resolve 'react-native' in the project. " +
                  "Altenatively, you can specify 'reactNativeDir' with the path to 'react-native' in your 'gradle.properties' file."
  )
}

def reactNativeRootDir = resolveReactNativeDirectory()
def reactProperties = new Properties()
file("$reactNativeRootDir/ReactAndroid/gradle.properties").withInputStream { reactProperties.load(it) }

def reactNativeVersion = reactProperties.getProperty("VERSION_NAME")
def (reactNativeMajorVersion, reactNativeMinorVersion) = reactNativeVersion.split("\\.").collect { it.isInteger() ? it.toInteger() : it }


def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.billiecamera"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildFeatures {
    androidResources = true
    viewBinding = true
  }
}

repositories {
  mavenCentral()
  google()
  if (reactNativeMajorVersion == 0 && reactNativeMinorVersion < 71) {
    def androidSourcesDir = file("$reactNativeRootDir/android")
    def androidSourcesName = "React Native sources"

    if (androidSourcesDir.exists()) {
      maven {
        url androidSourcesDir.toString()
        name androidSourcesName
      }
    }
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {

  implementation "androidx.core:core-ktx:1.6.0"
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2")
  implementation("androidx.appcompat:appcompat:1.2.0")
  implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.7.0")
  implementation("androidx.concurrent:concurrent-futures-ktx:1.2.0")

  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  if (reactNativeMajorVersion == 0 && reactNativeMinorVersion < 71) {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"
    // From node_modules
  } else {
    implementation "com.facebook.react:react-android:+"
  }


  // CameraX core library
  def camerax_version = "1.3.4"
  //    // CameraX view library
  //    def camerax_view_version = "1.0.0-alpha24"
  implementation("androidx.camera:camera-core:1.3.4")
  // If you want to use Camera2 extensions
  implementation("androidx.camera:camera-camera2:1.3.4")
  // If you to use the Camera View class
  implementation("androidx.camera:camera-view:1.3.4")
  // If you to use the Camera View class
  implementation("androidx.camera:camera-video:1.3.4")
  // CameraX Lifecycle library
  implementation("androidx.camera:camera-lifecycle:1.3.4")
  // CameraX video
  implementation("androidx.camera:camera-video:1.3.4")

  implementation("androidx.constraintlayout:constraintlayout:2.1.4")

  // PhotoView
  implementation("com.github.chrisbanes:PhotoView:2.3.0")
  implementation("com.github.getActivity:XXPermissions:18.65")

  // 基础依赖包，必须要依赖
  implementation("com.geyifeng.immersionbar:immersionbar:3.2.2")
  // kotlin扩展（可选）
  implementation("com.geyifeng.immersionbar:immersionbar-ktx:3.2.2")
  // Gson
  implementation("com.google.code.gson:gson:2.11.0")
}


if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "BillieCamera"
    codegenJavaPackageName = "com.billiecamera"
  }
}
